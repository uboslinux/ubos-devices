developer="https://github.com/milesp20/"
url="https://github.com/milesp20/intel_nuc_led"
maintainer="http://indiecomputing.com/"
pkgname=$(basename $(pwd))
pkgver=0.3
pkgrel=1
pkgdesc='Intel NUC7i[x]BN and NUC6CAY LED Control for Linux '
arch=('x86_64')
license=('GPL3')
depends=('linux>=4.15' 'linux<4.16')
makedepends=('linux-headers>=4.15' 'linux-headers<4.16')
_extramodules=extramodules-4.15-ARCH
source=(
    "origin::git+https://github.com/milesp20/intel_nuc_led.git#commit=1ee419dd154a8e99007de5e740f8b4c181c903ec"
)
sha512sums=('SKIP')
releasepage=(${url})

build() {
    cd origin
    make KVERSION="$(cat /usr/lib/modules/${_extramodules}/version)"
}

package() {
    install -m644 origin/nuc_led.ko -Dt "${pkgdir}/usr/lib/modules/${_extramodules}"
    find "${pkgdir}" -name '*.ko' -exec gzip -n {} +

    echo nuc_led | install -Dm644 /dev/stdin "${pkgdir}/usr/lib/modules-load.d/nuc_led.conf"
}
