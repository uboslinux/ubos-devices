# Maintainer: Donald Webster <fryfrog@gmail.com>
# Contributor: Edmunt Pienkowsky <roed@onet.eu>
# From AUR

releasepage=('https://aur.archlinux.org/cgit/aur.git/tree/PKGBUILD?h=rpi-eeprom')
pkgverforked=('2020.09.03-1')

pkgname=rpi-eeprom
pkgver=2020.09.03
_pkghash=138a1
pkgrel=1
pkgdesc='Raspberry Pi4 boot EEPROM updater'
arch=('armv7h' 'aarch64')
url='http://github.com/raspberrypi/rpi-eeprom'
license=('custom')
depends=(
  'python'
  'binutils'
)
makedepends=(
  'perl'
)
optdepends=(
  'raspberrypi-firmware: Use this on an armv7h (32bit) system.'
  'raspberrypi-userland-aarch64: Use this on an aarch64 (64bit) system.'
)

backup=('etc/default/rpi-eeprom-update')
source=("rpi-eeprom-${pkgver}-${_pkghash}.tar.gz::https://github.com/raspberrypi/rpi-eeprom/archive/v${pkgver}-${_pkghash}.tar.gz"
        'rpi-eeprom-update.patch')
sha512sums=('3f20b07bc5d243baf6db6393c0e41e303b2b14a5fbd49bc07c142c2d3f79ef8cf79d53f711a0f17fb9c48566186cea09bf61ce41a37a13c072d0e6c66a2fd48e'
            '731b3f92863ebe80f9f489e28121721bb8f3bd7b1116a409251d629e59be39bd14f1c51c0a6409fa39e22c06b96640390b316e3a14cf3805c8f58b80644dc910')
options=(!strip)

package() {
  # Create and install the binaries in /usr/bin
  install -d -m 755 "${pkgdir}/usr/bin"
  install -D -m 755 rpi-eeprom-${pkgver}-${_pkghash}/firmware/vl805 "${pkgdir}/usr/bin/vl805"
  install -D -m 755 rpi-eeprom-${pkgver}-${_pkghash}/rpi-eeprom-config "${pkgdir}/usr/bin/rpi-eeprom-config"

  # Patch /opt/vc/bin into PATH in script due to weird install location of raspberrypi-firmware
  install -D -m 755 rpi-eeprom-${pkgver}-${_pkghash}/rpi-eeprom-update "${pkgdir}/usr/bin/rpi-eeprom-update"
  patch "${pkgdir}/usr/bin/rpi-eeprom-update" "${srcdir}/rpi-eeprom-update.patch"

  # Copy all the firmware and files to the right place
  install -d -m 755 "${pkgdir}/usr/lib/firmware/raspberrypi/bootloader"
  cp -a rpi-eeprom-${pkgver}-${_pkghash}/firmware/* "${pkgdir}/usr/lib/firmware/raspberrypi/bootloader"

  # Copy the override in place
  install -d -m 755 "${pkgdir}/etc/default"
  install -D -m 644 rpi-eeprom-${pkgver}-${_pkghash}/rpi-eeprom-update-default "${pkgdir}/etc/default/rpi-eeprom-update"

  # UBOS: use stable instead of critical
  perl -p -i -e 's!^FIRMWARE_RELEASE_STATUS=.*$!FIRMWARE_RELEASE_STATUS="stable"!' "${pkgdir}/etc/default/rpi-eeprom-update"
}
